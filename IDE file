#include <SoftwareSerial.h>

const int trigPin = 5;
const int echoPin = 6;
const int buzzerPin = 9;

// Bluetooth module on pins 10 (RX) and 11 (TX)
SoftwareSerial BTSerial(10, 11);

long duration;
float distance;
bool buzzerState = false;
unsigned long lastToggleTime = 0;
const unsigned long beepInterval = 1000;
bool alertSent = false;

void setup() {
  Serial.begin(9600);
  BTSerial.begin(9600);
  pinMode(trigPin, OUTPUT);
  pinMode(echoPin, INPUT);
  pinMode(buzzerPin, OUTPUT);
  digitalWrite(buzzerPin, LOW);
  Serial.println("Water Level Monitoring Started");
  BTSerial.println("System Ready: Monitoring Water Level");
}

void loop() {
  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);

  duration = pulseIn(echoPin, HIGH, 60000);
  if (duration == 0) {
    Serial.println("No echo detected");
    BTSerial.println("Sensor Error: No echo detected");
    digitalWrite(buzzerPin, LOW);
    alertSent = false;
    return;
  }

  distance = (duration * 0.0343) / 2.0;
  Serial.print("Distance: ");
  Serial.print(distance);
  Serial.println(" cm");

  if (distance > 150 || distance < 21) {
    unsigned long currentTime = millis();
    if (currentTime - lastToggleTime >= beepInterval) {
      buzzerState = !buzzerState;
      digitalWrite(buzzerPin, buzzerState ? HIGH : LOW);
      lastToggleTime = currentTime;
    }

    Serial.println("ALERT: Water level is outside safe range!");
    if (!alertSent) {
      BTSerial.println("ALERT: Water level is outside safe range!");
      alertSent = true;
    }
  } else {
    digitalWrite(buzzerPin, LOW);
    buzzerState = false;
    alertSent = false;
    Serial.println("Safe water level");
    BTSerial.println(" Safe water level");
  }

  delay(100);
}
